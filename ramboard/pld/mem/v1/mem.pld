Name mem;
Partno G22V10#2;
Revision;
Date 09/1/2023;
Designer Scott Baker;
Company ;
Location ;
Assembly multibus ram mem;
Device G22V10;

/* Inputs: */
Pin 1  = !MSB0;
Pin 2  = MSRAM1;
Pin 3  = MSRAM2;
Pin 4  = MSROM1;
Pin 5  = MSROM2;
Pin 6  = MSBANK;
Pin 7  = !IOSEL;
Pin 8  = !INHIBIT;
Pin 9  = !MRDC;
Pin 10 = !MWTC;
Pin 11 = !IORC;
Pin 13 = !IOWC;

/* Outputs:  */
Pin 23 = !RAMRD;
Pin 22 = !RAMWR;
Pin 21 = !ROMRD;
Pin 20 = !ROMWR;
Pin 19 = !BOARDCS;
Pin 18 = BDIR;
Pin 17 = !IORD;
Pin 16 = !IOWR;
Pin 15 = NC1;
Pin 14 = NC2;

/* NOTE: intended to gate ROMCS and RAM0CS on MSB0, but found that
 * A16, A17, A18, and A19 are not implemented on the 80/24 board
 */

ROMCS = (MSROM1 # MSROM2) & (!INHIBIT);  /* ROM selected in bank 0 and not inhibited */
RAM0CS = (MSRAM1 # MSRAM2);			/* RAM selected in bank 0 */
RAMCS = (RAM0CS # MSBANK) & (!INHIBIT);         /* RAM selected in bank 0 or ram selected in high banks and not inhibited */

_RAMRD = RAMCS & (!ROMCS) & MRDC;
_RAMWR = RAMCS & MWTC;

_ROMRD = ROMCS & MRDC;
_ROMWR = 'b'0;

_IORD = IOSEL & IORC;
_IOWR = IOSEL & IOWC;

RAMRD = _RAMRD;
RAMWR = _RAMWR;
ROMRD = _ROMRD;
ROMWR = _ROMWR;

IORD = _IORD;
IOWR = _IOWR;

NC1 = RAMCS;
NC2 = ROMCS;

BOARDCS = _RAMRD # _RAMWR # _ROMRD # _ROMWR # _IORD # _IOWR;
BDIR = !(_RAMRD # _ROMRD # _IORD);
