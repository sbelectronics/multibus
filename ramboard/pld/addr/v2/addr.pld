Name addr;
Partno G22V10#2;
Revision;
Date 09/1/2023;
Designer Scott Baker;
Company ;
Location ;
Assembly multibus ram address;
Device G22V10;

/* Inputs: */
Pin 1  = IOWC;
Pin 2  = A0;
Pin 3  = A1;
Pin 4  = A2;
Pin 5  = A3;
Pin 6  = A4;
Pin 7  = A5;
Pin 8  = A6;
Pin 9  = A7;
Pin 10 = NC10;
Pin 11 = !MPST01;
Pin 13 = !MPST23;

/* Outputs:  */
Pin 23 = !CONFIGWR;
Pin 22 = !IOSEL;
Pin 21 = NC21;
Pin 20 = !MINT_CS;
Pin 19 = !INT_CS;
Pin 18 = !IOC_CS;
Pin 17 = !MCS0;
Pin 16 = !MCS1;
Pin 15 = !MCS2;
Pin 14 = !MCS3;

FIELD ADDR = [A7..A0];

/* SBC-80/24A Ports
 *
 * D4 - power fail
 * D5 - system bus
 * D6 - LED
 * DA/D8 and DB/D9 - onboard 8259
 * DC-DF - counter/timer
 * E4,E5,E6,E7 - onboard 8255-1
 * E8,E9,EA,EB - onboard 8255-2
 * EC, ED - onboard 8251
 * C0-CF - multimodule J5
 * F0-FF - multimodule J6
 *
 * With multimodules: C0-CF, D4-FF
 * Without multimodules: D4-EF
 */

 /* IPC and IOC ports
  *
  * 68-6F - hard drive board, optional
  * 78-7F - first floppy board, optional
  * 88-8F - second floppy board, optional
  *
  * C0/C1 - IOC
  * F8/F9 - PIO
  *
  * F0 - serial 0 baud
  * F1 - serial 1 baud
  * F2 - RTC
  * F3 - timer
  * F4 - serial 0 data
  * F5 - serial 0 status
  * F6 - serial 1 data
  * F7 - serial 1 status
  * FA, FB - local int
  * FC, FD - system int
  * FF - control port
  *
  * Master ints
  * int0 - monitor
  * int1 - isis-II
  * int2 - disk
  * int3 - unused
  * int4 - ice-80
  * int5 - unused
  * int6 - ice
  * int7 - local int controller
  *
  * IOC ints
  * int0 - SIO0 in
  * int1 - SIO0 out
  * int2 - SIO1 in
  * int3 - SIO1 out
  * int4 - 1ms RTC
  * int5 - PIO
  * int6 - IOC
  * int7 - unused
  */

/* rightmost multimodule socket */

_MCS0 = MPST01 & ((ADDR:B0) # (ADDR:B1) # (ADDR:B2) # (ADDR:B3) # (ADDR:B4) # (ADDR:B5) # (ADDR:B6) # (ADDR:B7));
_MCS1 = MPST01 & ((ADDR:B8) # (ADDR:B9) # (ADDR:BA) # (ADDR:BB) # (ADDR:BC) # (ADDR:BD) # (ADDR:BE) # (ADDR:BF));

/* leftmost multimodule socket */

_MCS2 = MPST23 & ((ADDR:A0) # (ADDR:A1) # (ADDR:A2) # (ADDR:A3) # (ADDR:A4) # (ADDR:A5) # (ADDR:A6) # (ADDR:A7));
_MCS3 = MPST23 & ((ADDR:A8) # (ADDR:A9) # (ADDR:AA) # (ADDR:AB) # (ADDR:AC) # (ADDR:AD) # (ADDR:AE) # (ADDR:AF));

_CPUC_CS = (ADDR:FF);
_INT_CS = (ADDR:FA) # (ADDR:FB);
_MINT_CS = (ADDR:FC) $ (ADDR:FD);
_IOC_CS = (ADDR:C0) # (ADDR:C1);

MCS0 = _MCS0;
MCS1 = _MCS1;
MCS2 = _MCS2;
MCS3 = _MCS3;

IOC_CS = _IOC_CS;
INT_CS = _INT_CS;
MINT_CS = _MINT_CS;

CONFIGWR = _CPUC_CS & IOWC; /* write strobe to config reg */

/* NOTE: IOC_CS and CPUC_CS do not use IOSEL as they are connected directly to the inverted data bus */

IOSEL = _MCS0 # _MCS1 # _MCS2 # _MCS3 # _INT_CS # MINT_CS;

