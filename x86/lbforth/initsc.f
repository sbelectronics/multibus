: 's' 115 ;
: '"' 34 ;
: ':' 58 ;
: ';' 59 ;
: "'" 39 ;
: CELL+ CELL + ;
: CELL- CELL - ;
: DECIMAL 10 BASE ! ;
: HEX 16 BASE ! ;
: OCTAL 8 BASE ! ;
: 2DUP OVER OVER ;
: 2DROP DROP DROP ;
: NIP SWAP DROP ;
: 2NIP 2SWAP 2DROP ;
: TUCK SWAP OVER ;
: / /MOD NIP ;
: MOD /MOD DROP ;
: BL 32 ;
: CR 10 EMIT ;
: SPACE BL EMIT ;
: NEGATE 0 SWAP - ;
: DNEGATE 0. 2SWAP D- ;
: CELLS CELL * ;
: ALLOT HERE @ + HERE ! ;
: TRUE -1 ;
: FALSE 0 ;
: 0= 0 = ;
: 0< 0 < ;
: 0> 0 > ;
: <> = 0= ;
: <= > 0= ;
: >= < 0= ;
: 0<= 0 <= ;
: 0>= 0 >= ;
: 1+ 1 + ;
: 1- 1 - ;
: 2+ 2 + ;
: 2- 2 - ;
: 2/ 2 / ;
: 2* 2 * ;
: D2/ 2. D/ ;
: +! DUP @ ROT + SWAP ! ;
: [COMPILE] WORD FIND >CFA , ; IMMEDIATE
: [CHAR] key ' LIT , , ; IMMEDIATE
: RECURSE LATEST @ >CFA , ; IMMEDIATE
: DOCOL 0 ;
: CONSTANT CREATE DOCOL , ' LIT , , ' EXIT , ;
: 2CONSTANT SWAP CREATE DOCOL , ' LIT , , ' LIT , , ' EXIT , ;
: VARIABLE HERE @ CELL ALLOT CREATE DOCOL , ' LIT , , ' EXIT , ;
: 2VARIABLE HERE @ 2 CELLS ALLOT CREATE DOCOL , ' LIT , , ' EXIT , ;
: IF ' 0BRANCH , HERE @ 0 , ; IMMEDIATE
: THEN DUP HERE @ SWAP - SWAP ! ; IMMEDIATE
: ELSE ' BRANCH , HERE @ 0 , SWAP DUP HERE @ SWAP - SWAP ! ; IMMEDIATE
: BEGIN HERE @ ; IMMEDIATE
: UNTIL ' 0BRANCH , HERE @ - , ; IMMEDIATE
: AGAIN ' BRANCH , HERE @ - , ; IMMEDIATE
: WHILE ' 0BRANCH , HERE @ 0 , ; IMMEDIATE
: REPEAT ' BRANCH , SWAP HERE @ - , DUP HERE @ SWAP - SWAP ! ; IMMEDIATE
: UNLESS ' 0= , [COMPILE] IF ; IMMEDIATE
: DO HERE @ ' SWAP , ' >R , ' >R , ; IMMEDIATE
: LOOP ' R> , ' R> , ' SWAP , ' 1+ , ' 2DUP , ' = , ' 0BRANCH , HERE @ - , ' 2DROP , ; IMMEDIATE
: +LOOP ' R> , ' R> , ' SWAP , ' ROT , ' + , ' 2DUP , ' <= , ' 0BRANCH , HERE @ - , ' 2DROP , ; IMMEDIATE
: I ' R@ , ; IMMEDIATE
: SPACES DUP 0> IF 0 DO SPACE LOOP ELSE DROP THEN ;
: ABS DUP 0< IF NEGATE THEN ;
: DABS 2DUP 0. D< IF DNEGATE THEN ;
: .DIGIT DUP 9 > IF 55 ELSE 48 THEN + EMIT ;
: .SIGN DUP 0< IF 45 EMIT NEGATE THEN ;
: .POS BASE @ /MOD ?DUP IF RECURSE THEN .DIGIT ;
: . .SIGN DUP IF .POS ELSE .DIGIT THEN ;
: COUNTPOS SWAP 1 + SWAP BASE @ / ?DUP IF RECURSE THEN ;
: DIGITS DUP 0< IF 1 ELSE 0 THEN SWAP COUNTPOS ;
: .R OVER DIGITS - SPACES . ;
: . . SPACE ;
: ? @ . ;
: .S DSP@ BEGIN DUP S0@ > WHILE DUP ? CELL - REPEAT DROP ;
: TYPE 0 DO DUP C@ EMIT 1 + LOOP DROP ;
: ALIGN BEGIN HERE @ CELL MOD WHILE 0 C, REPEAT ;
: xs" ' LITSTRING , HERE @ 0 , BEGIN KEY DUP 34 <> WHILE C, REPEAT DROP DUP HERE @ SWAP - CELL - SWAP ! ALIGN ; IMMEDIATE
: s" IMMEDIATE STATE @ IF ' LITSTRING , HERE @ 0 , BEGIN KEY DUP '"' <> WHILE C, REPEAT DROP DUP HERE @ SWAP - CELL- SWAP
       ! ALIGN ELSE HERE @ BEGIN KEY DUP '"' <> WHILE OVER C! 1+ REPEAT DROP HERE @ - HERE @ SWAP THEN ;
: ." [COMPILE] s" ' TYPE , ; IMMEDIATE
: ( BEGIN KEY [CHAR] ) = UNTIL ; IMMEDIATE
: COUNT DUP 1+ SWAP C@ ;
: MIN 2DUP < IF DROP ELSE NIP THEN ;
: MAX 2DUP > IF DROP ELSE NIP THEN ;
: D0= OR 0= ;
: DMIN 2OVER 2OVER D< IF 2DROP ELSE 2NIP THEN ;
: DMAX 2OVER 2OVER D> IF 2DROP ELSE 2NIP THEN ;
: F_HIDDEN 64 ;
: F_IMMEDIATE 128 ;
: F_LENMASK 31 ;
: ID. CELL+ DUP C@ F_LENMASK AND BEGIN DUP 0> WHILE SWAP 1+ DUP C@ EMIT SWAP 1- REPEAT 2DROP ;
: ?HIDDEN CELL+ C@ F_HIDDEN AND ;
: ?IMMEDIATE CELL+ C@ F_IMMEDIATE AND ;
: WORDS LATEST @ BEGIN ?DUP WHILE DUP ?HIDDEN NOT IF DUP ID. SPACE THEN @ REPEAT CR ;
: CASE IMMEDIATE 0 ;
: OF IMMEDIATE ' OVER , ' = , [COMPILE] IF ' DROP , ;
: ENDOF IMMEDIATE [COMPILE] ELSE ;
: ENDCASE IMMEDIATE ' DROP , BEGIN ?DUP WHILE [COMPILE] THEN REPEAT ;
: PICK 1+ CELL * DSP@ SWAP - @ ;
: >DFA >CFA CELL+ ;
: CFA> LATEST @ BEGIN ?DUP WHILE 2DUP >CFA SWAP = IF NIP EXIT THEN @ REPEAT DROP 0 ;
: ALIGNED 3 + 3 NOT AND ;
: LITERAL IMMEDIATE ' LIT , , ;
: SEE WORD FIND HERE @ LATEST @ BEGIN 2 PICK OVER <> WHILE NIP DUP @ REPEAT DROP SWAP ':' EMIT SPACE DUP ID. SPACE
  DUP ?IMMEDIATE IF ." IMMEDIATE " THEN >DFA BEGIN 2DUP > WHILE DUP @ CASE 
  ' LIT OF CELL+ DUP @ . ENDOF
  ' LITSTRING OF 's' EMIT '"' EMIT SPACE CELL+ DUP @ SWAP CELL+ SWAP 2DUP TELL '"' EMIT SPACE + ALIGNED CELL- ENDOF
  ' 0BRANCH OF ." 0BRANCH ( " CELL+ DUP @ . ." ) " ENDOF
  ' BRANCH OF ." BRANCH ( " CELL+ DUP @ . ." ) " ENDOF
  ' ' OF "'" EMIT SPACE CELL+ DUP @ CFA> ID. SPACE ENDOF
  ' EXIT OF 2DUP CELL+ <> IF ." EXIT " THEN ENDOF
  DUP CFA> ID. SPACE ENDCASE CELL+ REPEAT ';' EMIT CR 2DROP ;
: VALUE CREATE DOCOL , ' LIT , , ' EXIT , ;
: TO IMMEDIATE WORD FIND >DFA CELL+ STATE @ IF ' LIT , , ' ! , ELSE ! THEN ;
: +TO IMMEDIATE WORD FIND >DFA CELL+ STATE @ IF ' LIT , , ' +! , ELSE +! THEN ;
: ['] IMMEDIATE ' LIT , ;
: BANNER ." READY" CR ;
BANNER
