;	TITLE	'drives`
;	Scott Baker, www.smbaker.com
;
;	Print drive device list for Multibus ISIS
;	Not sure if addresses are right for iPDS
;
;	Makes use of the following ISIS variables
;	   DKCFTB - table of device formats (1=DD, 2=SD, 3=ISD, 4=HDD)
;	   DKCTTB - table of controllers (1=78, 2=88, 3=C1, 4=68)
;	   UNITMP - table of unit select bits
;
;
; ISIS system calls
;
	EXTRN COUT
	EXTRN CIN
	EXTRN ZSOUT
	EXTRN COPEN
	EXTRN EXIT

	EXTRN PSPACE
	EXTRN PHEXA
	EXTRN PBINA
	EXTRN PCRLF

DKCFTB	EQU	07A0H
DKCTTB	EQU	07BEH
UNITMP	EQU	07B4H

CR      EQU      0DH            ;CARRIAGE RETURN
LF      EQU      0AH            ;LINE FEED

	STKLN	100H				; Size of stack segment

	CSEG	

ORIG:	LXI	SP, STACK
	CALL	COPEN

	LXI	D, BANNER
	CALL	ZSOUT

	LXI	D, F0MSG	; A CLEVERER PROGRAMMER MIGHT HAVE USED A LOOP...
	CALL	ZSOUT		; ... I'M NOT IN A CLEVER MOOD.
	LDA	DKCTTB
	CALL	PCTRL
	LDA	DKCFTB
	CALL	PFMT
	LDA	UNITMP
	CALL	PUNIT
	CALL	PCRLF

	LXI	D, F1MSG
	CALL	ZSOUT
	LDA	DKCTTB+1
	CALL	PCTRL
	LDA	DKCFTB+1
	CALL	PFMT
	LDA	UNITMP+1
	CALL	PUNIT
	CALL	PCRLF

	LXI	D, F2MSG
	CALL	ZSOUT
	LDA	DKCTTB+2
	CALL	PCTRL
	LDA	DKCFTB+2
	CALL	PFMT
	LDA	UNITMP+2
	CALL	PUNIT
	CALL	PCRLF

	LXI	D, F3MSG
	CALL	ZSOUT
	LDA	DKCTTB+3
	CALL	PCTRL
	LDA	DKCFTB+3
	CALL	PFMT
	LDA	UNITMP+3
	CALL	PUNIT
	CALL	PCRLF

	LXI	D, F4MSG
	CALL	ZSOUT
	LDA	DKCTTB+4
	CALL	PCTRL
	LDA	DKCFTB+4
	CALL	PFMT
	LDA	UNITMP+4
	CALL	PUNIT
	CALL	PCRLF

	LXI	D, F5MSG
	CALL	ZSOUT
	LDA	DKCTTB+5
	CALL	PCTRL
	LDA	DKCFTB+5
	CALL	PFMT
	LDA	UNITMP+5
	CALL	PUNIT
	CALL	PCRLF

	CALL	EXIT

PCTRL:	CPI	0
	JNZ	PCTRL1
	LXI	D, NOTIM
	JMP	PCTRLP
PCTRL1:	CPI	1
	JNZ	PCTRL2
	LXI	D, S202M
	JMP	PCTRLP
PCTRL2: CPI	2
	JNZ	PCTRL3
	LXI	D, S201M
	JMP	PCTRLP
PCTRL3:	CPI	3
	JNZ	PCTRL4
	LXI	D, ISDM
	JMP	PCTRLP
PCTRL4:	CPI	4
	JNZ	PCTRL5
	LXI	D, HDDM
	JMP	PCTRLP
PCTRL5: LXI	D, UNKM
PCTRLP:	JMP	ZSOUT

PFMT:	CPI	0
	JNZ	PFMT1
	LXI	D, NOTIM
	JMP	PFMTP
PFMT1:	CPI	1
	JNZ	PFMT2
	LXI	D, DDM
	JMP	PFMTP
PFMT2: CPI	2
	JNZ	PFMT3
	LXI	D, SDM
	JMP	PFMTP
PFMT3:	CPI	3
	JNZ	PFMT4
	LXI	D, ISDM
	JMP	PFMTP
PFMT4:	CPI	4
	JNZ	PFMT5
	LXI	D, HDDM
	JMP	PFMTP
PFMT5: 	LXI	D, UNKM
PFMTP:	JMP	ZSOUT

PUNIT:	CALL	PSPACE
	CALL	PSPACE
	CALL	PSPACE
	CALL	PHEXA
	RET

BANNER: DB	'DRIVE LIST',0DH,0AH
	DB	'SCOTT BAKER, WWW.SMBAKER.COM',0DH,0AH,0DH,0AH
	;; falls through to header

HDRM:	DB	'DEV '
	DB	' CONTROLLER    '
	DB	' FORMAT        '
	DB	' UNIT',0DH,0AH
	DB	'===='
	DB	' =============='
	DB	' =============='
	DB	' ====',0DH,0AH,0

F0MSG:  DB	':F0:',0
F1MSG:  DB	':F1:',0
F2MSG:  DB	':F2:',0
F3MSG:  DB	':F3:',0
F4MSG:  DB	':F4:',0
F5MSG:  DB	':F5:',0

UNKM:	DB	' UNKNOWN       ',0
S201M:	DB	' SBC-201       ',0
S202M:	DB	' SBC-202       ',0
ISDM:	DB	' ISD           ',0
HDDM:	DB	' HDD           ',0
NOTIM:  DB	' NOT-INSTALLED ',0

DDM:	DB	' DOUBLE-DENSITY',0
SDM:	DB	' SINGLE-DENSITY',0

	END	ORIG
